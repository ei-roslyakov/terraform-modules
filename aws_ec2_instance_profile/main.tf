resource "aws_iam_instance_profile" "main" {
  name = "${var.name}-profile"
  path = var.path
  role = aws_iam_role.main.name
}

resource "aws_iam_role" "main" {
  name               = "${var.name}-role"
  path               = var.path
  description        = "Role assigned to the ${var.name} profile instance profile (generated by Terraform)"
  assume_role_policy = data.aws_iam_policy_document.assume_role.json
  tags = merge(
    {
      "Name" = format("%s", var.name)
    },
    var.tags,
  )
}

data "aws_iam_policy_document" "assume_role" {
  statement {
    effect = "Allow"

    principals {
      type        = "Service"
      identifiers = ["ec2.amazonaws.com"]
    }

    actions = ["sts:AssumeRole"]
  }
}


resource "aws_iam_role_policy_attachment" "attached_policies" {
  for_each = toset(var.policy_arns)

  role       = aws_iam_role.main.name
  policy_arn = each.key
}